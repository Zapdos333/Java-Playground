// plugins
apply plugin: 'java'
//project properties
project.setVersion('4.0.0')
java.toolchain.languageVersion = JavaLanguageVersion.of(17)
// projects
subprojects {
	apply plugin: 'java'
	project.setVersion(project.getRootProject().getVersion())
	sourceSets.main.java.destinationDirectory = file("${project.getRootDir()}/bin/${project.getName()}")
	buildDir = file("${project.getRootDir()}/build/${project.getName()}")
	java.toolchain.languageVersion = project.getRootProject().java.toolchain.languageVersion
	compileJava() {}
	jar {
		archiveFileName = "${project.getRootProject().getName()}-${project.getName()}-${project.getVersion()}.jar"
		destinationDirectory.set(file("${project.getRootDir()}/build/libs/"))
		duplicatesStrategy = 'exclude'
	}
	processResources.onlyIf { false }
	javadoc.onlyIf { false }
}
/*
project(':template'){
	sourceSets.main.java.srcDirs=["${project.getRootDir()}/src/java/${project.getName()}"]
	dependencies {}
	jar.from configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
}
*/
project(':library-general'){
	sourceSets.main.java.srcDirs=["${project.getRootDir()}/src/java/${project.getName()}"]
	dependencies {} // no dependencies allowed
	jar.from configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
}
project(':library-CClass'){
	sourceSets.main.java.srcDirs=["${project.getRootDir()}/src/java/${project.getName()}"]
	dependencies {} // no dependencies allowed
	jar.from configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
}
project(':library-Math'){
	sourceSets.main.java.srcDirs=["${project.getRootDir()}/src/java/${project.getName()}"]
	dependencies {
		implementation project(':library-CClass')
	}
	jar.from configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
}
project(':library-CoordinateSystem'){
	sourceSets.main.java.srcDirs=["${project.getRootDir()}/src/java/${project.getName()}"]
	dependencies {
		implementation project(':library-CClass')
		implementation project(':library-Math')
	}
	jar.from configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
}
project(':library'){ //secondary jar
	dependencies {
		implementation project(':library-general')
		implementation project(':library-CClass')
		implementation project(':library-CoordinateSystem')
		implementation project(':library-Math')
	}
	jar.from configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
}
project(':nonLibrary-HelloWorld'){
	sourceSets.main.java.srcDirs=["${project.getRootDir()}/src/java/${project.getName()}"]
	dependencies {}
	jar.from configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
}
project(':nonLibrary-school'){
	sourceSets.main.java.srcDirs=["${project.getRootDir()}/src/java/${project.getName()}"]
	dependencies {
		implementation project(':library-general')
		implementation project(':library-CClass')
		implementation project(':library-Math')
	}
	jar.from configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
}
project(':nonLibrary-tests'){
	sourceSets.main.java.srcDirs=["${project.getRootDir()}/src/java/${project.getName()}"]
	dependencies {
		implementation project(':library')
		implementation project(':nonLibrary-school')
	}
	jar.from configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
}
project(':nonLibrary'){ //secondary jar
	dependencies {
		implementation project(':nonLibrary-HelloWorld')
		implementation project(':nonLibrary-school')
		implementation project(':nonLibrary-tests')
	}
	jar.from configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
	jar.manifest.attributes("Main-Class": "com.Ace009.nonLibrary.debug.CommandLineWrapper")
}
//root dependencies
dependencies {
	implementation project(':library-general')
	implementation project(':library-CoordinateSystem')
	implementation project(':library-CClass')
	implementation project(':library-Math')
	implementation project(':nonLibrary-HelloWorld')
	implementation project(':nonLibrary-school')
	implementation project(':nonLibrary-tests')
}
//main jar
jar {
	archiveFileName = "${project.archivesBaseName}-${project.getVersion()}.jar"
	from configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
	duplicatesStrategy = 'exclude'
	destinationDirectory.set(file("./build/libs"))
}
//set main classpath
sourceSets {
	main.java {
		srcDirs=[
			file("./src/java/library-general/"),
			file("./src/java/library-CClass/"),
			file("./src/java/library-CoordinateSystem/"),
			file("./src/java/library-Math/"),
			file("./src/java/nonLibrary-HelloWorld/"),
			file("./src/java/nonLibrary-school/"),
			file("./src/java/nonLibrary-tests/")
		]
	}
}
//configure root
buildDir="./build/"
//configure build scan (--scan)
if (hasProperty('buildScan')) {
	buildScan {
		tag "Java-Playground"
		tag "v"+project.getVersion()
		link "github dev-branch", "https://github.com/Zapdos333/Java-Playground/tree/dev"
		termsOfServiceUrl = "https://gradle.com/terms-of-service"
        termsOfServiceAgree = "yes"
	}
}
//configure javadocs
javadoc {
	destinationDir=file("${project.getRootDir()}/build/docs")
	source = project.getRootProject().sourceSets.main.java.srcDirs
	options.memberLevel = JavadocMemberLevel.PRIVATE
	options.addStringOption('Xdoclint:all,-reference,-missing','-quiet')
}
task checkDocs(type: Javadoc) {
	group = "documentation"
	description = "checks the Javadocs"
	outputs.upToDateWhen { false }
	destinationDir=file("${project.getRootDir()}/build/checkDocs")
	source = project.getRootProject().sourceSets.main.java.srcDirs
	options.memberLevel = JavadocMemberLevel.PRIVATE
	options.addStringOption('Xdoclint:all')
	doLast {
		delete "${project.getRootDir()}/build/checkDocs"
	}
}
task packageDocs(type: Zip) {
	group = "documentation"
	description = "packs the javadocs into a zip file in the libs folder"
	dependsOn javadoc
	archiveFileName="${project.archivesBaseName}-JavaDocs-${project.getVersion()}.zip"
	destinationDirectory=file("${project.getRootDir()}/build/libs")
	from file("${project.getRootDir()}/build/docs")
	entryCompression = "deflated"
}
build.dependsOn packageDocs
//debug
task printCheck(){
	group = "verification"
	description = "debug print"
	doLast{
		println()
	}
}
//commented out(line1) until I get (line2) to work
	//check.dependsOn checkDocs
	//build.dependsOn -= check
// cleanUp
clean {
	description = "deletes the build, bin, .history and __pycache__ directory"
	delete ".history", "bin", "src/python/__pycache__"
}
